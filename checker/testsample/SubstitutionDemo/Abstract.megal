module Abstract

/* Serializer */
?PL : ProgrammingLanguage;
    encodedAs Text;
    facilitates ObjectOrientedProgramming.
?Serializer : Library;
    uses ?PL;
    implements ?PL;
    facilitates Serialization.

/* Object serialization */
?OL : ValueLanguage;
    encodes Object.
?D : DataExchangeLanguage;
    encodedAs Text;
    encodedAs Markup.
f : ?OL -> ?D.

/* conformance to data types in an OO program */
?project : Application;
    uses ?PL;
    reuses ?Serializer;
    uses Serialization.
?program : Artifact;
    elementOf ?PL;
    hasRole Schema;
    hasRole POJO;
    manifestsAs File+;
    defines ?OL.
?object : Artifact;
    elementOf ?OL;
    manifestsAs Transient;
    hasRole Database;
    conformsTo ?program.

/* Actual serialization */
?DS : DataExchangeLanguage;
    subsetOf ?D.
?program defines ?DS.
?data : Artifact;
    elementOf ?DS;
    hasRole Database;
    manifestsAs File;
    correspondsTo ?object.
f(?object) |-> ?data.
?Serializer implements f.