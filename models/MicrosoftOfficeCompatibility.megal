module MicrosoftOfficeCompatibility

import C
import XML

/* The old office suite Microsoft Office 2003 written in C++ is occasionally still in use.
It still uses the deprecated binary format for persisting office documents. */
OfficeWare : TechnologySpace;
    = "https://www.reference.com/business-finance/definition-office-technology-7627b30083ad168a".

MicrosoftOffice2003 : SoftwareSuite ;
    = "https://en.wikipedia.org/wiki/Microsoft_Office";
    uses C++;
    belongsTo OfficeWare.

/* The microsoft office compatibility plugin enables a user to edit files
written in the new XML format with Office 2003. */
MicrosoftOfficeOpenXML : DocumentLanguage ;
    = "https://en.wikipedia.org/wiki/Office_Open_XML";
    subsetOf XML.
CFBF : DocumentLanguage;
    = "https://en.wikipedia.org/wiki/Compound_File_Binary_Format";
    basedOn Binary;
    ^implements MicrosoftOffice2003.

/* It implements a conversion that transforms a file apparent in the new format
into the old format. A hint on this functionality is given at:
https://en.wikipedia.org/wiki/Office_Open_XML#Application%20support */
convert :  MicrosoftOfficeOpenXML -> CFBF.
MicrosoftOfficeCompatibility : Plugin;
    = "http://www.pcworld.com/article/217190/microsoft_office_docx.html";
    uses C++; //pure guess since MicrosoftOffice2003 uses C++
    uses Transformation;
    implements convert.

/* Most parts of the actual XSD schemas for the new office format are available online. */
MicrosoftOfficeOpenXMLSchema : Artifact;
    = "https://msdn.microsoft.com/en-us/library/dd979954(v=office.12).aspx";
    elementOf XSD;
    hasRole Schema;
    manifestsAs File+;
    defines MicrosoftOfficeOpenXML.

/* In a concrete use case a user would load a document written in
the XML based format. If the plugin is installed in the used Microsoft Office 2003,
the document is converted and can then be edited. */
?aNewOfficeFile : Artifact;
    elementOf MicrosoftOfficeOpenXML;
    hasRole Document;
    manifestsAs File;
    conformsTo MicrosoftOfficeOpenXMLSchema.
?anOldOfficeFile : Artifact;
    elementOf CFBF;
    hasRole Document;
    manifestsAs File.
convert(?aNewOfficeFile) |-> ?anOldOfficeFile .